plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
}

group = 'dev.ursinn.paper.nici'
version = '1.0-SNAPSHOT'
description = 'PaperNiciManagement'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.18.2-R0.1-SNAPSHOT'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// Javadoc
javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

// Publishing Stack - Start
java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'management'
            from components.java
            pom {
                packaging = 'jar'
                name = 'PaperNiciManagement'
                description = 'Management Plugin'
                url = 'https://github.com/ursinn/paper-nici-management'
                inceptionYear = '2022'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/ursinn/paper-nici-management/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'ursinn'
                        name = 'Ursin Filli'
                        email = 'mail@ursinn.dev'
                        timezone = '+1'
                        url = 'https://ursinn.dev'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ursinn/paper-nici-management.git'
                    developerConnection = 'scm:git:git@github.com:ursinn/paper-nici-management.git'
                    url = 'https://github.com/ursinn/paper-nici-management'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/ursinn/paper-nici-management/issues'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhS01Username
                password ossrhS01Password
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

// Publishing Stack - End